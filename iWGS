#!/usr/bin/env perl
use strict;
use warnings;

use Cwd;
use FindBin;
use Getopt::Long;

use lib ("$FindBin::Bin/lib");
use Ctrl;
use Reads;
use Preprocess;
use Assembly;
use Utilities;

my $usage = "
iWGS: in silico Whole Genome Sequencer and Analyzer
Version 1.0

Usage: iWGS [options]

Basic options:
-s | --settings	  <string>	the contrl file that contains all settings
-g | --genome	  <string>	the reference genome sequence in FASTA format
-t | --threads	  <int>		number of CPUs to use (default: 1)
-m | --memory	  <int>		number of GBs of memeory to use (default: 8)
-o | --out_dir	  <string>	the directory to store all outputs, will be created if not already exist (default \"./\")
-c | --cleanup	  		whether to clean up after all steps are finished (default: 0 [no])
-r | --overwrite  		whether to overwrite if a library already exists (default: 0 [no])
-v | --verify	  		verify the settings and quit
-h | --help			print this usage message

Advanced options:
--Mode				iWGS can run in the following three modes: (default: 1)
				1: 	get parameters from command line / control file, and finish all steps (reads simulation,
					assembly, evaluation) in a single run
				2: 	get parameters from command line / control file, and quit after generating three configuration
					files for libraries, assembly protocols, and miscellaneous options, respectively. These
					configuration files would allow each library/protocol to have different parameters.
				3: 	restart from the configuration files generated in Mode 2.
--Conf				the configuration files for libraries, assembly protocols, and misellaneous options, separated by comma.
--Real				run iWGS with real datasets

Please report any problem to \"xiaofan.zhou\@vanderbilt.edu\".
";

unless (@ARGV)	{
	die "$usage\n";
}

#############################
# setting parameters
#############################

my $global_ctrl_file;
my $cleanup = 0;
my $overwrite = 0;
my $verify = 0;
my $help = 0;
my $mode = 1;
my $real = 0;
my $conf;
my %opt;

&GetOptions(
	'settings|s=s' => \$global_ctrl_file,
	'genome|g=s' => \$opt{'genome'},
	'threads|t=i' => \$opt{'threads'},
	'memory|m=i' => \$opt{'memory'},
	'out_dir|o=s' => \$opt{'out_dir'},
	'cleanup|c' => \$cleanup,
	'overwrite|r' => \$overwrite,
	'verify|v' => \$verify,
	'help|h' => \$help,
	"Mode=i" => \$mode,
	"Conf=s" => \$conf,
	"Real" => \$real,
);

if ($help)	{
	die "$usage\n";
}

# initialize global options with default settings

print "\n##############################\n# iWGS Stage 1: configuration\t\t".localtime()."\n##############################\n\n";

my %global_opt;
if ($mode ==1 || $mode == 2)	{
	%global_opt = &Ctrl::init_global_opt();
	# set global options using command line arguments
	&Ctrl::override_opt(\%opt, \%global_opt);
	# set global options using the global control file
	if (defined($global_ctrl_file))	{
		unless (-e $global_ctrl_file)	{
			die "ERROR: Please make sure the global control file is available!\n";
		}
		&Ctrl::read_global_ctrl_file($global_ctrl_file, \%global_opt);
	}
	# validate global options
	&Ctrl::check_global_opt(\%global_opt, $real);
	# if running in Mode 2, generate configuration files and quit
	if ($mode == 2)	{
		&Ctrl::write_conf_file(\%global_opt);
		die "Successfully generated configuration files for libraries, assembly protocols, and miscellaneous options!\nThe current iWGS run in Mode $mode finished!\n";
	}	
}	elsif ($mode == 3)	{
	my @conf = split /,/, $conf;
	unless (@conf && $#conf == 2 && -e $conf[0] && -e $conf[1] && -e $conf[2])	{
		die "ERROR: In Mode $mode, please make sure there are three configuration files and they are all available!\n";
	}
	# if running with Mode 3, set lib, prot, and misc options using the corresponding configuration files
	%global_opt = &Ctrl::read_conf_file(@conf);
	&Ctrl::check_global_opt(\%global_opt, $real);
}	else	{
	die "Invalid value of \"Mode\", should be one of following: \"1\", \"2\", and \"3\".\n";
}

if ($verify)	{
	print "All configurations verified, the iWGS run finished!\n\n";
	exit;
}	else	{
	print "All configurations verified, now proceed to the next stage!\n\n";
}


#############################
# reads simulation
#############################

print "##############################\n# iWGS Stage 2: reads simulation\t".localtime()."\n##############################\n\n";

unless (-d $global_opt{'out_dir'})	{
	mkdir($global_opt{'out_dir'});
}

unless (-d "$global_opt{'out_dir'}/libraries")	{
	mkdir("$global_opt{'out_dir'}/libraries");
}

unless (-d "$global_opt{'out_dir'}/logs")	{
	mkdir("$global_opt{'out_dir'}/logs");
}

chdir("$global_opt{'out_dir'}/libraries");

foreach my $library (sort keys %{$global_opt{'library'}})	{
	my $skip = 0;
	
	unless (-d "$global_opt{'out_dir'}/libraries/$library")	{
		mkdir("$global_opt{'out_dir'}/libraries/$library");
	}
	chdir("$global_opt{'out_dir'}/libraries/$library");

	# convert all read data to Phred33
	if ($real == 1)	{
		$skip = &Preprocess::prepare_real($library, \%global_opt, $overwrite);
	}		
		
	# skip the simulation if overwrite if off and the library already exists
	if ($overwrite == 0)	{
		if ($global_opt{'library'}->{$library}->{'read_type'} eq "se" || $global_opt{'library'}->{$library}->{'read_type'} eq "clr")	{
			if (-e "$library.fq")	{
				print "NOTE: the library $library already exists, skip to the next.\n\n";
				$skip = 1;
			}
		}	else	{
			if (-e "$library\_1.fq" && -e "$library\_2.fq")	{
				print "NOTE: the library $library already exists, skip to the next.\n\n";
				$skip = 1;
			}
		}
	}

	next if ($skip);

	# report error if the library is to be simulated but the genome is not provided
	if ($real == 1)	{
		unless (defined($global_opt{'genome'}) && -e $global_opt{'genome'}) { die "The current iWGS run includes at least one simulated dataset (e.g. ".$library."). Make sure the reference genome is provided.\n"; }
	}
	
	# run simulation
	&Reads::simulation($library, \%global_opt);
}


#############################
# quality control
#############################

# check if QC is turned on for at least one library
my @qc;
foreach my $library (sort keys %{$global_opt{'library'}})	{
	if ($global_opt{'library'}->{$library}->{'qc'})	{
		push @qc, $library
	}
}

if (@qc)	{
	print "##############################\n# iWGS Stage 2.5: quality control\t".localtime()."\n##############################\n\n";

	unless (-d "$global_opt{'out_dir'}/preprocessed")	{
		mkdir("$global_opt{'out_dir'}/preprocessed");
	}

	foreach my $library (@qc)	{
		unless (-d "$global_opt{'out_dir'}/preprocessed/$library")	{
			mkdir("$global_opt{'out_dir'}/preprocessed/$library");
		}
		chdir("$global_opt{'out_dir'}/preprocessed/$library");
	
		&Preprocess::QC($library, \%global_opt, $overwrite);
	}
}

#############################
# de novo assembly
#############################

print "##############################\n# iWGS Stage 3: de novo assembly\t".localtime()."\n##############################\n\n";

unless (-d "$global_opt{'out_dir'}/protocols")	{
	mkdir("$global_opt{'out_dir'}/protocols");
}

unless (-d "$global_opt{'out_dir'}/assemblies")	{
	mkdir("$global_opt{'out_dir'}/assemblies");
}

foreach my $protocol (sort keys %{$global_opt{'protocol'}})	{
	unless (-d "$global_opt{'out_dir'}/protocols/$protocol")	{
		mkdir("$global_opt{'out_dir'}/protocols/$protocol");
	}
	chdir("$global_opt{'out_dir'}/protocols/$protocol");

	&Assembly::assemble($protocol, \%global_opt, $overwrite, $cleanup);	
}

#############################
# assembly evaluation
#############################

print "##############################\n# iWGS Stage 4: evaluation\t".localtime()."\n##############################\n\n";

# remove the evaluation folder if it already exists
if (-d "$global_opt{'out_dir'}/evaluation")	{
	system("rm -rf $global_opt{'out_dir'}/evaluation");
}
mkdir("$global_opt{'out_dir'}/evaluation");

# perform QUAST evaluation on assemblies
&Assembly::quast("assemblies", \%global_opt);

if (defined($global_opt{'reapr'}->{'short'}) && defined($global_opt{'reapr'}->{'long'}))	{
	# perform REAPR correction
	unless (-d "$global_opt{'out_dir'}/assemblies.cor")	{
		mkdir("$global_opt{'out_dir'}/assemblies.cor");
	}
	chdir("$global_opt{'out_dir'}/assemblies.cor");
	&Assembly::reapr(\%global_opt);
	
	# perform QUAST evaluation on REAPR corrected assemblies
	if (-d "$global_opt{'out_dir'}/evaluation.cor")	{
		system("rm -rf $global_opt{'out_dir'}/evaluation.cor");
	}
	mkdir("$global_opt{'out_dir'}/evaluation.cor");
	&Assembly::quast("assemblies.cor", \%global_opt);
}

# estimate cost
&Utilities::calculate_price(\%global_opt);


#############################
# Clean up
#############################

if ($cleanup)	{
	system("rm -r $global_opt{'out_dir'}/libraries $global_opt{'out_dir'}/protocols");
	if (-d "$global_opt{'out_dir'}/preprocessed")	{
		system("rm -r $global_opt{'out_dir'}/preprocessed");
	}
}

#############################
# DONE
#############################

print "##############################\n# iWGS is done!\t\t".localtime()."\n##############################\n\n";

exit;
